# オプション
options:
    gui: "武器・装備の設定"
    guilvlup: "武器・装備の強化"
    sub: "&7 [ サブステータス枠 ]"


function generateWeapon(id: text) :: item:
    set {_Rarity} to {Weapon::%{_id}%::Rarity}
    set {_Type} to {Weapon::%{_id}%::Type}
    set {_Name} to "%{MaterialColor::%{_Rarity}%}%%{Weapon::%{_id}%::Name}%"

    if {Weapon::%{_id}%::Itemtype} doesn't contain "none":
        set {_ItemType} to {Weapon::%{_id}%::Itemtype} parsed as itemtype
        parse error is set
        set {_ItemType} to {WType::%{_Type}%::Item}
    else:
        set {_ItemType} to {WType::%{_Type}%::Item}
    
    set {_I} to unbreakable {_ItemType} with all flags hidden named {_Name}
    set {_l::1} to "&0%{_ID}%;%random integer between 0 to 99999999%"

    #
    # 武器ステータスの設定
    #

    loop indices of {Stat::*}:
        add loop-value to {_stat.%{Stat::%loop-value%::Rarity}%::*}

    if {WType::%{_Type}%::Type} is "weapon":
        set {_l::2} to "&f● 基礎攻撃力 %{Weapon::%{_id}%::Power}%"
        set {_l::3} to "&f"

    #   メインステータス
    add "&f&m     &f メインステータス &m     " to {_l::*}
    add "&f" to {_l::*}
    set {_main} to {Weapon::%{_id}%::Main}
    remove "%{_main}%" from {_stat.%{Stat::%{_main}%::Rarity}%::*}

    set {_main.name} to "%{Stat::%{_main}%::Color}%%{Stat::%{_main}%::Symbol}% %{Stat::%{_main}%::Name}%"

    set {_main.value} to {Weapon::%{_id}%::Main.Amount}
    if {Stat::%{_main}%::Percent} is 1:
        set {_main.value} to "%{_main.value}%%%"
    else if {Stat::%{_main}%::Percent} is 2:
        {Weapon::%{_id}%::Main.isPercent} is "true":
            set {_main.value} to "%{_main.value}%%%"
            set {_tempsub.%{_main}%} to 1
        else:
            set {_tempsub.%{_main}%} to 0

    add "&0%{_main.name}%&f %{_main.value}%" to {_l::*}
    add "&f" to {_l::*}

    #   サブステータス
    add "&7&m     &7 サブステータス &m       " to {_l::*}
    add "&f" to {_l::*}
    set {_Rare::*} to 5, max(0, {_rarity} - 1) and max(0, {_rarity}*2 - 7)
    set {_Raresum} to sum({_Rare::*})
    set {_Subs} to min(4, random integer between ({_Rarity}-1) and {_Rarity})
    set {_SubMax} to max(min(4, {_Rarity}), 2)
    loop {_subs} time:
        set {_SubRand} to random integer between 1 to {_Raresum}
        set {_SubRarity} to 1
        loop {_Rare::*}:
            remove {_Rare::%loop-index%} from {_SubRand}
            {_SubRand} <= 0
            set {_SubRarity} to loop-index
            exit a loop
        set {_sub} to random element out of {_stat.%{_SubRarity}%::*}
        set {_sub.name} to "%{Stat::%{_sub}%::Color}%%{Stat::%{_sub}%::Symbol}% %{Stat::%{_sub}%::Name}%"
        set {_sub.value} to 5
        if {_subrarity} is "3":
            set {_sub.name} to "&9★%{_sub.name}%"
        if {Stat::%{_sub}%::Percent} is 2:
            if {_tempsub.%{_sub}%} is set:
                {_tempsub.%{_sub}%} is 0:
                    set {_sub.value} to "%{_sub.value}%%%"
                remove {_sub} from {_stat.%{_SubRarity}%::*}
            else:
                chance of 50%:
                    set {_sub.value} to "%{_sub.value}%%%"
                    set {_tempsub.%{_sub}%} to 1
                else:
                    set {_tempsub.%{_sub}%} to 0
        else if {Stat::%{_sub}%::Percent} is 1:
            set {_sub.value} to "%{_sub.value}%%%"
            remove {_sub} from {_stat.%{_SubRarity}%::*}
        add "&0%{_sub.name}%&f %{_sub.value}%" to {_l::*}
    loop {_SubMax} - {_Subs} times:
        add {@sub} to {_l::*}
    add "&f" to {_l::*}
    
    #
    # 最終処理
    #
    add "&7« &e%{Rarity::%{Weapon::%{_id}%::Rarity}%}% &7%{WType::%{_Type}%}% &7»" to {_l::*}
    set {_formatted} to now formatted with "yy/MM/dd HH:mm:ss"
    add "&8%{_formatted}%" to {_l::*}
    set {_i}'s lore to {_l::*}
    add nbt of "{model:%{Weapon::%{_id}%::Model}%}" to nbt of {_i}
    return {_i}


function getWeaponSample(id: text) :: item:
    set {_Rarity} to {Weapon::%{_id}%::Rarity}
    set {_Type} to {Weapon::%{_id}%::Type}
    set {_Name} to "%{MaterialColor::%{_Rarity}%}%%{Weapon::%{_id}%::Name}%"

    if {Weapon::%{_id}%::Itemtype} doesn't contain "none":
        set {_ItemType} to {Weapon::%{_id}%::Itemtype} parsed as itemtype
        parse error is set
        set {_ItemType} to {WType::%{_Type}%::Item}
    else:
        set {_ItemType} to {WType::%{_Type}%::Item}
    
    set {_I} to unbreakable {_ItemType} with all flags hidden named {_Name}
    set {_l::1} to "&0%{_ID}%;SAMPLEITEM"

    #
    # 武器ステータスの設定
    #

    if {WType::%{_Type}%::Type} is "weapon":
        set {_l::2} to "&f● 基礎攻撃力 %{Weapon::%{_id}%::Power}%"
        set {_l::3} to "&f"

    #   メインステータス
    add "&f&m     &f メインステータス &m     " to {_l::*}
    add "&f" to {_l::*}
    set {_main} to {Weapon::%{_id}%::Main}
    set {_main.name} to "%{Stat::%{_main}%::Color}%%{Stat::%{_main}%::Symbol}% %{Stat::%{_main}%::Name}%"
    set {_main.value} to {Weapon::%{_id}%::Main.Amount}
    if {Stat::%{_main}%::Percent} is 1:
        set {_main.value} to "%{_main.value}%%%"
    else if {Stat::%{_main}%::Percent} is 2:
        {Weapon::%{_id}%::Main.isPercent} is "true"
        set {_main.value} to "%{_main.value}%%%"

    add "&0%{_main.name}%&f %{_main.value}%" to {_l::*}
    add "&f" to {_l::*}

    #   サブステータス
    add "&7&m     &7 サブステータス &m       " to {_l::*}
    add "&f" to {_l::*}
    set {_SubMax} to max(min(4, {_Rarity}), 2)
    loop {_SubMax} times:
        add {@sub} to {_l::*}
    add "&f" to {_l::*}
    
    #
    # 最終処理
    #
    add "&7« &e%{Rarity::%{Weapon::%{_id}%::Rarity}%}% &7%{WType::%{_Type}%}% &7»" to {_l::*}
    set {_i}'s lore to {_l::*}
    add nbt of "{model:%{Weapon::%{_id}%::Model}%}" to nbt of {_i}
    return {_i}

function AttributeItem(i: item, type: text, amount: number, operation: int = 0, slot: texts = ("mainhand")) :: item:
    replace all ".nl" with nl in {_Type}
    set {_1} to "{AttributeName:""generic.%colored {_type}%"",Name:""generic.%colored {_type}%"","
    set {_2} to "Amount:%{_amount}%,Operation:%{_operation}%,UUIDLeast:1,UUIDMost:999998,"
    loop {_slot::*}:
        add 1 to {_}
        if {_} is amount of {_slot::*}:
            set {_3} to "Slot:""%loop-value%""}"
        else:
            set {_3} to "Slot:""%loop-value%""},%{_1}%%{_2}%"
    broadcast "{AttributeModifiers:[%{_1}%%{_2}%%{_3}%]}"
    add nbt of "{AttributeModifiers:[%{_1}%%{_2}%%{_3}%]}" to nbt of {_i}
    return {_i}
command /attribute <text> <number> [<int>] [<text>]:
    permission: op
    trigger:
        set {_op} to arg-3 ? 0
        set {_slot::*} to arg-4 split at "," ? "mainhand"
        set player's tool to AttributeItem(player's tool, arg-1, arg-2, {_op},{_slot::*})
tab complete of "/attribute":
    set {_1::*} to "attackSpeed", "attackDamage", "maxHealth", "followRange", "knockbackResistance" and "armor"
    add "movementSpeed", "armorToughness", "attackKnockback", "luck", "jumpStrength", "flyingSpeed" and "spawnReinforcements"  to {_1::*}
    set tab completions for position 1 to {_1::*}




function checkWeaponSeed(i: item) :: item:
    if tag "weaponseed" of nbt of {_i} is set:
        set {_id} to tag "weaponseed" of nbt of {_i}
        broadcast "WeaponSeed: %{_id}%"
        return generateWeapon({_id})
    return {_i}